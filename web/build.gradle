apply plugin: 'kotlin2js'
apply plugin: 'kotlin-dce-js'
apply plugin: 'org.jetbrains.kotlin.frontend'

repositories {
    jcenter()
}

dependencies {
    implementation project(":common")

    implementation Libs.kotlinStdlibJs
    testImplementation Libs.kotlinTestJs

    implementation Libs.kotlinxHtmlJs
    implementation Libs.kotlinxCoroutinesCoreCommon
    implementation Libs.kotlinxCoroutinesCoreJs
    implementation Libs.kotlinxSerializationRuntimeJs

    implementation Libs.kotlinExtensions
    implementation Libs.kotlinReact
    implementation Libs.kotlinReactDom
}

kotlinFrontend {

    downloadNodeJsVersion = Versions.node

    sourceMaps = false

    npm {
        replaceVersion "kotlinx-html-js", Versions.kotlin
        replaceVersion "kotlinx-html-shared", Versions.kotlin
        replaceVersion "kotlin-js-library", Versions.kotlin

        dependency "react", Versions.react
        dependency "react-dom"

        // wont be needed once https://github.com/ktorio/ktor/issues/961 is fixed
        dependency "text-encoding"

        devDependency "webpack", "4"
        devDependency "babel-loader", "8"
        devDependency "@babel/core"
        devDependency "@babel/preset-env"
        devDependency "css-loader"
        devDependency "karma"
        devDependency "style-loader"
        devDependency "source-map-loader"
        devDependency "html-loader"
    }

    webpackBundle {
        publicPath = "/frontend/"
        port = 8080
        proxyUrl = "http://localhost:9090/"
        sourceMapEnabled = true
        stats = "errors-only"
    }
}

compileKotlin2Js {
    kotlinOptions.metaInfo = true
    kotlinOptions.outputFile = "$project.buildDir.path/js/${project.name}.js"
    kotlinOptions.sourceMap = true
    kotlinOptions.sourceMapEmbedSources = "always"
    kotlinOptions.moduleKind = 'commonjs'
    kotlinOptions.main = "call"
}
